;
; Copyright (c) 2023 Raspberry Pi (Trading) Ltd.
;
; SPDX-License-Identifier: BSD-3-Clause
;

.program encoder

; the code must be loaded at address 0, because it uses computed jumps
.origin 0

; 00 state
    JMP update    ; read 00
    JMP decrement ; read 01
    JMP increment ; read 10
    JMP update    ; read 11
; 01 state
    JMP increment ; read 00
    JMP update    ; read 01
    JMP update    ; read 10
    JMP decrement ; read 11
; 10 state
    JMP decrement ; read 00
    JMP update    ; read 01
    JMP update    ; read 10
    JMP increment ; read 11
; 11 state
    JMP update    ; read 00
    JMP increment ; read 01
decrement:
    JMP Y--, update [2] ; read 10
.wrap_target
update:
    MOV ISR, Y      ; read 11
    PUSH noblock

    OUT ISR, 2
    JMP !X, sample_pins2
sample_pins3:
    IN PINS, 1
    IN PINS, 3
    JMP movosr
sample_pins2:
    IN PINS, 2
    IN PINS, 2
movosr:
    MOV OSR, ISR
    OUT NULL, 2
    MOV PC, OSR
increment:
    MOV Y, ~Y
    JMP Y--, increment_cont
increment_cont:
    MOV Y, ~Y
.wrap    ; the .wrap here avoids one jump instruction and saves a cycle too
